import com.timberglund.gradle.plugin.asciidoctor.FrameworkTask

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.timberglund:asciidoctor-presentation:0.1.1"
    }
}
plugins {
    id "org.ajoberstar.git-publish" version "0.2.0"
}

apply plugin: "com.timberglund.presentation.plugin"
apply plugin: 'application'

repositories {
    jcenter()
}
dependencies {
    runtime 'me.champeau.deck2pdf:deck2pdf:0.3.0'
}

mainClassName = "me.champeau.deck2pdf.Main"
run {
    inputs.dir "src"
    outputs.file "$buildDir/slides.pdf"
    dependsOn tasks.presentation
    args "$buildDir/asciidoc/deckjs/slides.html", "$buildDir/slides.pdf"
}

task pdf {
    dependsOn run
}

def customextensions =
[
//    presenterview: 'https://github.com/stvnwrgs/presenterview',
//    split: 'https://github.com/houqp/deck.split.js',
    zoom: 'https://github.com/grayside/deck.zoom.js',
]
customextensions.each { name, uri ->
    tasks.create(name, FrameworkTask) {
      frameworkDir = "deck.js/extensions/$name"
      delegate.uri = uri
    }
    tasks.framework.dependsOn name
}

task customjs {
    doLast {
        file('src/framework/deck.js/extensions/customjs/customjs.js').with {
            parentFile.mkdirs()
            withPrintWriter { pw ->
                customextensions.each { name, uri ->
                    def jsFile = file("src/framework/deck.js/extensions/$name/deck.${name}.js")
                    if (jsFile.exists()) {
                        pw << jsFile.text.replace("extensions/", "deckjs/deck.js/extensions/")
                        pw.println()
                    }
                }
            }
        }
    }
}
task customcss {
    doLast {
        file('src/framework/deck.js/extensions/customcss/customcss.css').with {
            parentFile.mkdirs()
            withPrintWriter { pw ->
                customextensions.each { name, uri ->
                    def cssFile = file("src/framework/deck.js/extensions/$name/deck.${name}.css")
                    if (cssFile.exists()) {
                        pw << cssFile.text
                        pw.println()
                    }
                }
            }
        }
    }
}
tasks.framework.finalizedBy customjs, customcss

presentation {
    modules {
        content {
            title = "The content"
            file = "content.adoc"
        }
    }
}

slides {
    headers = [
        deckjs_transition: 'fade',
        deckjs_theme: 'tschulte',
        scale: '',
        navigation: '',
        menu: '',
        goto: '',
        status: '',
        split: '',
        customjs: 'deck.js/extensions/customjs/customjs.js',
        customcss: 'deck.js/extensions/customcss/customcss.css',
    ]
    title = "Semantic Release"
    author = "Tobias Schulte"
}

lessc {
    sourceDir "src/styles"
    include "**/*.less"
    destinationDir = "${buildDir}/asciidoc/deckjs/deck.js/themes/style"
    mustRunAfter slides
}

gitPublish {
    repoUri = "git@github.com:tschulte/gradle-semantic-release-plugin-presentation"
    // branch will be created if it doesn't exist
    branch = 'gh-pages'

    // what to publish, this is a standard CopySpec
    contents {
        from "$buildDir/asciidoc/deckjs/"
    }
    // message used when committing changes
    commitMessage = 'Publishing a version of the presentation' // defaults to 'Generated by gradle-git-publish'
}
gitPublishCopy.dependsOn tasks.presentation
