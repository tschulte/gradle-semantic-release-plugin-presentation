plugins {
    id "org.ajoberstar.git-publish" version "0.3.2"
    id 'org.asciidoctor.convert' version '1.5.6'
    id 'com.github.jruby-gradle.base' version '1.5.0'
}

apply plugin: 'org.asciidoctor.convert'

ext {
    revealjsVersion = '3.6.0'
    asciidoctorBackendVersion = '1.1.3'
    downloadDir = new File(buildDir,'download')
}

repositories {
    jcenter()
    ivy {
        name = "github"
        url = "https://github.com/"
        layout("pattern") {
            artifact "[organization]/[artifact]/archive/[revision].[ext]"
        }
    }
    ivy {
        name = "github-with-v"
        url = "https://github.com/"
        layout("pattern") {
            artifact "[organization]/[artifact]/archive/v[revision].[ext]"
        }
    }
}

configurations {
    asciidoctorBackend
    revealjs
    pdf
}

dependencies {
    pdf 'me.champeau.deck2pdf:deck2pdf:0.3.0'
    asciidoctorBackend "asciidoctor:asciidoctor-reveal.js:$asciidoctorBackendVersion@zip"
    revealjs "hakimel:reveal.js:$revealjsVersion@zip"
}

task configureDownload {
    doLast {
        tasks.download.from zipTree(configurations.asciidoctorBackend.singleFile)
        tasks.download.from zipTree(configurations.revealjs.singleFile)
    }
}

task download(type: Sync) {
    dependsOn configureDownload
    into downloadDir
}

asciidoctorj {
  version = '1.5.6'
}

asciidoctor {
    inputs.file 'build.gradle'
    dependsOn download
    sourceDir 'src/slides'
    sources {
        include 'index.adoc'
    }
    resources {
        from (file('src')) {
            include 'images/**'
        }
        from (new File(downloadDir, "reveal.js-$revealjsVersion")) {
            include '**'
            into "/reveal.js"
        }
        from(file('src/styles')) {
            include '*.css'
        }
    }
    backends 'revealjs'
    attributes \
        'source-highlighter': 'coderay',
        'toc': 'left',
        'icons': 'font',
        'setanchors': '',
        'idprefix': '',
        'idseparator': '-',
        'docinfo1': '',
        customcss: 'tschulte.css',
        'revealjs_theme': 'white',
        'revealjs_transition': 'linear',
        'revealjs_history': 'true',
        'revealjs_slideNumber': 'true'
    options template_dirs: [new File(downloadDir, "asciidoctor-reveal.js-$asciidoctorBackendVersion/templates").absolutePath]
}

task pdf(type: JavaExec) {
    inputs.dir "src"
    outputs.file "$buildDir/slides.pdf"
    dependsOn tasks.asciidoctor
    args "$buildDir/asciidoc/revealjs/index.html", "$buildDir/index.pdf"
    classpath = configurations.pdf
    main = "me.champeau.deck2pdf.Main"
}

gitPublish {
    repoUri = "git@github.com:tschulte/gradle-semantic-release-plugin-presentation"
    // branch will be created if it doesn't exist
    branch = 'gh-pages'

    // what to publish, this is a standard CopySpec
    contents {
        from asciidoctor
    }
    // message used when committing changes
    commitMessage = 'Publishing a version of the presentation' // defaults to 'Generated by gradle-git-publish'
}
